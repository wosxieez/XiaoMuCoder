package com.xiaomu.view.roleview
{
	import com.xiaomu.component.ListWithMouseWheel;
	import com.xiaomu.data.RoleData;
	import com.xiaomu.event.RoleEvent;
	import com.xiaomu.manager.RoleManager;
	import com.xiaomu.renderer.RoleItemRenderer;
	
	import flash.events.Event;
	import flash.net.URLRequest;
	
	import coco.component.HorizontalAlign;
	import coco.component.List;
	import coco.component.Panel;
	import coco.component.VerticalAlign;
	import coco.event.UIEvent;
	import coco.manager.PopUpManager;
	import coco.net.URLLoader;
	
	public class RoleCollection extends Panel
	{
		public function RoleCollection()
		{
			width = 860;
			height = 560;
			title = "角色库"
			borderAlpha = backgroundAlpha = 1;
			borderColor = 0xF0E7CC;
			backgroundColor = 0xFEFBF0;
		}
		
		private var roleList:List;
		
		override protected function createChildren():void
		{
			super.createChildren();
			
			roleList = new ListWithMouseWheel();
			roleList.gap = roleList.padding = 20;
			roleList.itemRendererColumnCount = 4;
			roleList.itemRendererHeight = roleList.itemRendererWidth = 165;
			roleList.itemRendererClass = RoleItemRenderer;
			roleList.verticalAlign = VerticalAlign.TOP;
			roleList.horizontalAlign = HorizontalAlign.CENTER;
			roleList.addEventListener(UIEvent.CHANGE, roleList_changeHandler);
			addChild(roleList);
			
			// 加载所有的角色模版数据
			var urlLoader:URLLoader = new URLLoader();
			urlLoader.addEventListener(Event.COMPLETE, urlLoader_completeHandler);
			urlLoader.load(new URLRequest("assets/role/roles.json"));
		}
		
		override protected function updateDisplayList():void
		{
			super.updateDisplayList();
			
			roleList.width = contentWidth;
			roleList.height = contentHeight - 20;
		}
		
		override protected function drawSkin():void
		{
			super.drawSkin();
//			graphics.clear();
//			graphics.lineStyle(1, 0xEEE6C9);
//			graphics.beginFill(0xFEFBF2);
//			graphics.drawRect(0, 0, width, height);
//			graphics.endFill();
		}
		
		protected function urlLoader_completeHandler(event:Event):void
		{
			trace("角色模版加载成功");
			
			var roles:Array = JSON.parse(event.currentTarget.data) as Array;
			var rolesData:Array = [];
			var roleData:RoleData;
			for each (var item:Object in roles)
			{
				roleData = new RoleData();
				roleData.name = item.name;
				roleData.source = item.source;
				rolesData.push(roleData);
			}
			
			roleList.dataProvider = rolesData;
		}
		
		protected function roleList_changeHandler(event:UIEvent):void
		{
			var roleEvent:RoleEvent = new RoleEvent(RoleEvent.ADD_ROLE);
			roleEvent.roleData = roleList.selectedItem as RoleData;
			RoleManager.getInstance().dispatchEvent(roleEvent);
			
			roleList.selectedIndex = -1; // 重置选中(必须)
			PopUpManager.removePopUp(this); // 关闭窗口
		}
		
	}
}