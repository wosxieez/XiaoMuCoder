package com.xiaomu.hook
{
	
	import flash.events.EventDispatcher;
	import flash.geom.Point;
	
	
	[Event(name="targetChanged", type="com.xiaomu.hook.HookEvent")]
	
	[Event(name="matchChanged", type="com.xiaomu.hook.HookEvent")]
	
	
	/**
	 * 向上的钩子 
	 */	
	public class Hook extends EventDispatcher
	{
		public function Hook()
		{
			super();
		}
		
		/**
		 * 钩子组件 
		 */		
		public var hookComponent:HookComponent;
		
		private var _targetHook:Hook;
		
		/**
		 * 对应的钩子
		 */
		public function get targetHook():Hook
		{
			return _targetHook;
		}
		
		/**
		 * @private
		 */
		public function set targetHook(value:Hook):void
		{
			if (_targetHook == value) return;
			_targetHook = value;
			
			dispatchEvent(new HookEvent(HookEvent.TARGET_CHANGED));
		}
		
		
		private var _matchHook:Hook;
		
		/**
		 * 匹配上的钩子 
		 */
		public function get matchHook():Hook
		{
			return _matchHook;
		}
		
		/**
		 * @private
		 */
		public function set matchHook(value:Hook):void
		{
			if (_matchHook == value) return;
			_matchHook = value;
			
			dispatchEvent(new HookEvent(HookEvent.MATCH_CHANGED));
		}
		
		/**
		 * 钩子的坐标位置 
		 */		
		public var x:Number = 0;
		public var y:Number = 0;
		
		/**
		 * 钩子是否匹配
		 * @param hook
		 * @return 
		 */		
		public function match(hook:Hook):Boolean
		{
			var thisPoint:Point = hookComponent.localToGlobal(new Point(0, 0));
			thisPoint.x = thisPoint.x + x;
			thisPoint.y = thisPoint.y + y;
			
			var thatPoint:Point = hook.hookComponent.localToGlobal(new Point(0, 0));
			thatPoint.x = thatPoint.x + hook.x;
			thatPoint.y = thatPoint.y + hook.y;
			
			if (Math.abs(thatPoint.x - thisPoint.x) <= 10 &&
				Math.abs(thatPoint.y - thisPoint.y) <= 10) 
				return true;
			else
				return false;
		}
		
	}
}