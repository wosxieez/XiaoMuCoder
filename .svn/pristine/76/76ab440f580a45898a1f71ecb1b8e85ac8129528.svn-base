package com.xiaomu.view.stageview
{
	import com.xiaomu.data.Actoin;
	import com.xiaomu.data.Role;
	
	import coco.component.Image;
	import coco.core.UIComponent;
	
	public class RoleComponent extends UIComponent
	{
		public function RoleComponent()
		{
			super();
			
			mouseChildren = false;
			width = 100;
			height = 100;
		}
		
		
		//----------------------------------------------------------------------------------------------------------------
		//
		// Properties
		//
		//----------------------------------------------------------------------------------------------------------------
		
		private var imgDisplay:Image;
		
		private var _role:Role;

		public function get role():Role
		{
			return _role;
		}

		public function set role(value:Role):void
		{
			_role = value;
			invalidateProperties();
		}

		
		/**
		 * 脚本数据 
		 */		
		public var actions:Vector.<Actoin> = new Vector.<Actoin>();
		
		
		//----------------------------------------------------------------------------------------------------------------
		//
		// Methods
		//
		//----------------------------------------------------------------------------------------------------------------
		
		override protected function createChildren():void
		{
			super.createChildren();
			imgDisplay = new Image();
			addChild(imgDisplay);
		}
		
		override protected function commitProperties():void
		{
			super.commitProperties();
			
			if (role)
			{
				imgDisplay.source = role.source;
			}
			else
				imgDisplay.source = null;
		}
		
		override protected function updateDisplayList():void
		{
			super.updateDisplayList();
			
			imgDisplay.width = width;
			imgDisplay.height = height;
		}
		
		override protected function drawSkin():void
		{
			super.drawSkin();
			graphics.clear();
			graphics.beginFill(0x0000ff,0);
			graphics.drawRect(0,0,width,height);
			graphics.endFill();
		}
		
		/**
		 * 运行脚本动作
		 */		
		public function doAction():void
		{
			for each(var action:Actoin in actions)
			{
				action.doAction(this, null);
			}
		}
		
	}
}