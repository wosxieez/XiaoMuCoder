package com.xiaomu.view.actionview
{
	import com.xiaomu.actioncomponent.DoWhileActionComponent;
	import com.xiaomu.actioncomponent.MoveXActionComponent;
	import com.xiaomu.actioncomponent.StartActionComponent;
	import com.xiaomu.component.ActionComponent;
	import com.xiaomu.manager.ActionComponentManager;
	
	import flash.events.MouseEvent;
	import flash.geom.Point;
	import flash.utils.getDefinitionByName;
	import flash.utils.getQualifiedClassName;
	
	import coco.component.Group;
	import coco.component.HorizontalAlign;
	import coco.layout.VerticalLayout;
	import coco.manager.PopUpManager;
	
	
	/**
	 * 脚本列表视图 
	 * @author coco
	 */	
	public class ActionList extends Group
	{
		public function ActionList()
		{
			super();
			
			var vlayout:VerticalLayout = new VerticalLayout();
			vlayout.padding = vlayout.gap = 20;
			vlayout.horizontalAlign = HorizontalAlign.CENTER;
			layout = vlayout;
			
			autoDrawSkin = true;
			borderColor = 0xF0E7CC;
			backgroundColor = 0xF9F4E6;
			addEventListener(MouseEvent.MOUSE_DOWN, this_mouseDownHandler);
		}
		
		
		//----------------------------------------------------------------------------------------------------------------
		//
		// Methods
		//
		//----------------------------------------------------------------------------------------------------------------
		
		override protected function createChildren():void
		{
			super.createChildren();
			
			var startAction:StartActionComponent = new StartActionComponent();
			startAction.mouseChildren = false;
			addChild(startAction);
			
			var moveXAction:MoveXActionComponent = new MoveXActionComponent();
			moveXAction.y = 50;
			moveXAction.mouseChildren = false;
			addChild(moveXAction);
			
			var moveXAction2:MoveXActionComponent = new MoveXActionComponent();
			moveXAction2.y = 150;
			moveXAction2.mouseChildren = false;
			addChild(moveXAction2);
			
			var doWhileAction:DoWhileActionComponent = new DoWhileActionComponent();
			doWhileAction.y = 250;
			doWhileAction.mouseChildren = false;
			addChild(doWhileAction);
		}
		
		protected function this_mouseDownHandler(event:MouseEvent):void
		{
			var actionComponent:ActionComponent = event.target as ActionComponent;
			if (actionComponent)
			{
				var globalPoint:Point = actionComponent.localToGlobal(new Point(0, 0));
				
				var actionComponentClass:Class = getDefinitionByName(getQualifiedClassName(actionComponent)) as Class;
				var newActionComponent:ActionComponent = new actionComponentClass() as ActionComponent;
				newActionComponent.x = globalPoint.x;
				newActionComponent.y = globalPoint.y;
				ActionComponentManager.getInstance().doDraggingActionComponent(newActionComponent);
				
				// close this view
				PopUpManager.removePopUp(this);
			}
		}
		
	}
}