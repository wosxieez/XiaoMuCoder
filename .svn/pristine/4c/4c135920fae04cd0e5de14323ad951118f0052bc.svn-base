package com.xiaomu.actioncomponent
{
	import com.xiaomu.component.ActionComponent;
	import com.xiaomu.component.DownActionHook;
	import com.xiaomu.event.ActionHookEvent;
	
	import coco.component.Label;
	
	
	/**
	 * 开始 脚本组件 
	 * @author coco
	 */	
	public class StartActionComponent extends ActionComponent
	{
		public function StartActionComponent()
		{
			super();
			
			width = 150;
			height = 30;
			
			downActionHook = new DownActionHook();
			downActionHook.addEventListener(ActionHookEvent.MATCH_CHANGED, matchChangedHandler);
			downActionHook.x = 15;
		}
		
		private var labelDisplay:Label;
		
		override protected function createChildren():void
		{
			super.createChildren();
			
			labelDisplay = new Label();
			labelDisplay.color = 0xFFFFFF;
			labelDisplay.text = "当开始被点击";
			addChild(labelDisplay);
		}
		
		override protected function updateDisplayList():void
		{
			super.updateDisplayList();
			
			downActionHook.y = height;
			
			labelDisplay.width = width;
			labelDisplay.height = height;
		}
		
		override protected function drawSkin():void
		{
			graphics.clear();
			graphics.beginFill(0x6A8EE7);
			graphics.drawRect(0, 0, width, height);
			graphics.endFill();
			graphics.beginFill(downActionHook.matchActionHook ? 0x00FF00 : 0x6A8EE7);
			graphics.drawRect(downActionHook.x, downActionHook.y, 10, 5);
			graphics.endFill();
		}
		
		protected function matchChangedHandler(event:ActionHookEvent):void
		{
			invalidateSkin();
		}
		
	}
}