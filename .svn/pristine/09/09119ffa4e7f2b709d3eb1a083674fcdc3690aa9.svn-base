package com.xiaomu.component
{
	
	import com.xiaomu.data.Hook;
	import com.xiaomu.event.ActionHookEvent;
	
	import flash.events.EventDispatcher;
	import flash.geom.Point;
	import flash.utils.getQualifiedClassName;
	
	
	[Event(name="targetChanged", type="com.xiaomu.event.ActionHookEvent")]
	
	[Event(name="matchChanged", type="com.xiaomu.event.ActionHookEvent")]
	
	
	/**
	 * 脚本钩子类 
	 */	
	public class HookComponent extends EventDispatcher
	{
		public function HookComponent()
		{
			super();
		}
		
		
		//----------------------------------------------------------------------------------------------------------------
		//
		// Properties
		//
		//----------------------------------------------------------------------------------------------------------------
		
		/**
		 * 钩子的坐标位置 
		 */		
		public var x:Number = 0;
		public var y:Number = 0;
		
		/**
		 * 钩子组件 
		 */		
		public var actionComponent:ActionComponent;
		
		private var _targetHookComponent:HookComponent;
		
		/**
		 * 对应的钩子
		 */
		public function get targetHookComponent():HookComponent
		{
			return _targetHookComponent;
		}
		
		/**
		 * @private
		 */
		public function set targetHookComponent(value:HookComponent):void
		{
			if (_targetHookComponent == value) return;
			_targetHookComponent = value;
			
			dispatchEvent(new ActionHookEvent(ActionHookEvent.TARGET_CHANGED));
		}
		
		
		private var _matchActionHook:HookComponent;
		
		/**
		 * 匹配上的钩子 
		 */
		public function get matchActionHook():HookComponent
		{
			return _matchActionHook;
		}
		
		/**
		 * @private
		 */
		public function set matchActionHook(value:HookComponent):void
		{
			if (_matchActionHook == value) return;
			_matchActionHook = value;
			
			dispatchEvent(new ActionHookEvent(ActionHookEvent.MATCH_CHANGED));
		}
		
		private var _hook:Hook = new Hook();

		public function get hook():Hook
		{
			return _hook;
		}

		
		//----------------------------------------------------------------------------------------------------------------
		//
		// Methods
		//
		//----------------------------------------------------------------------------------------------------------------
		
		/**
		 * 钩子是否匹配
		 * @param hook
		 * @return 
		 */		
		public function match(actionHook:HookComponent):Boolean
		{
			var thisPoint:Point = actionComponent.localToGlobal(new Point(0, 0));
			thisPoint.x = thisPoint.x + x;
			thisPoint.y = thisPoint.y + y;
			
			var thatPoint:Point = actionHook.actionComponent.localToGlobal(new Point(0, 0));
			thatPoint.x = thatPoint.x + actionHook.x;
			thatPoint.y = thatPoint.y + actionHook.y;
			
			if (Math.abs(thatPoint.x - thisPoint.x) <= 10 &&
				Math.abs(thatPoint.y - thisPoint.y) <= 10) 
				return true;
			else
				return false;
		}
		
		/**
		 * 将UI属性同步到data上 
		 */
		public function syncData():void
		{
			hook.className = getQualifiedClassName(this);
			hook.x = x;
			hook.y = y;
			hook.action = actionComponent.action;
			if (targetHookComponent)
				hook.targetHook = targetHookComponent.hook;
		}
		
	}
}