package com.xiaomu.manager
{
	import com.xiaomu.component.ActionComponent;
	import com.xiaomu.component.ActionHook;
	import com.xiaomu.component.DownActionHook;
	
	import flash.events.MouseEvent;
	import flash.geom.Point;
	
	import coco.core.Application;
	import coco.core.UIComponent;
	
	/**
	 * 钩子管理器 
	 * @author coco
	 * 
	 */	
	public class ActionComponentManager
	{
		public function ActionComponentManager()
		{
		}
		
		
		//----------------------------------------------------------------------------------------------------------------
		//
		// Properties
		//
		//----------------------------------------------------------------------------------------------------------------
		
		private var view:UIComponent;
		private var draggingHookComponents:Vector.<ActionComponent> = new Vector.<ActionComponent>();
		private var draggingFreeHooks:Vector.<ActionHook> = new Vector.<ActionHook>();
		private var matchingFreeHooks:Vector.<ActionHook> = new Vector.<ActionHook>();
		private var mouseDownX:Number = 0;
		private var mouseDownY:Number = 0;
		private var matchedDraggingHook:ActionHook;
		private var matchedMatchingHook:ActionHook;
		
		
		//----------------------------------------------------------------------------------------------------------------
		//
		// Get Instance
		//
		//----------------------------------------------------------------------------------------------------------------
		
		private static var instance:ActionComponentManager;
		
		public static function getInstance():ActionComponentManager
		{
			if (!instance)
				instance = new ActionComponentManager();
			
			return instance;
		}

		
		//----------------------------------------------------------------------------------------------------------------
		//
		// Methods
		//
		//----------------------------------------------------------------------------------------------------------------
		
		public function init(viewport:UIComponent):void
		{
			view = viewport;
			view.addEventListener(MouseEvent.MOUSE_DOWN, this_mouseDownHandler);
		}
		
		protected function this_mouseDownHandler(event:MouseEvent):void
		{
			doDraggingHookComponent(event.target as ActionComponent);
		}
		
		/**
		 * 开始拖拽一个钩子组件
		 * @param draggingHookComponent
		 */		
		public function doDraggingHookComponent(draggingHookComponent:ActionComponent):void
		{
			if (!draggingHookComponent) return;
			
			// 记录当前鼠标按下的点
			mouseDownX = view.stage.mouseX;
			mouseDownY = view.stage.mouseY;
			
			// 解锁超类钩子
			if (draggingHookComponent.upActionHook && draggingHookComponent.upActionHook.targetActionHook)
			{
				draggingHookComponent.upActionHook.targetActionHook.targetActionHook = null;
				draggingHookComponent.upActionHook.targetActionHook = null;
			}
			
			initDraggingHookComponents(draggingHookComponent); // 生成所有的拖拽钩子组件
			initDraggingFreeHooks(draggingHookComponent); // 生成空闲可匹配的钩子组
			initMatchingFreetHooks(); // 生成匹配目标的钩子组
			
			view.stage.addEventListener(MouseEvent.MOUSE_MOVE, stage_mouseMoveHandler);
			view.stage.addEventListener(MouseEvent.MOUSE_UP, stage_mouseUpHandler);
		}
		
		protected function stage_mouseMoveHandler(event:MouseEvent):void
		{
			for each (var draggingHookComponent:ActionComponent in draggingHookComponents)
			{
				draggingHookComponent.x = view.stage.mouseX - mouseDownX + draggingHookComponent.mouseDownX;
				draggingHookComponent.y = view.stage.mouseY - mouseDownY + draggingHookComponent.mouseDownY;
			}
			
			event.updateAfterEvent();
			invalidateMatchHook();
		}
		
		protected function stage_mouseUpHandler(event:MouseEvent):void
		{
			view.stage.removeEventListener(MouseEvent.MOUSE_MOVE, stage_mouseMoveHandler);
			view.stage.removeEventListener(MouseEvent.MOUSE_UP, stage_mouseUpHandler);
			
			// 如果有匹配上的钩子 则进行对接
			if (matchedDraggingHook && matchedMatchingHook)
			{
				// 如果匹配目标是向下的钩子且已经有对接的钩子 则属于插入操作
				if (matchedMatchingHook is DownActionHook && matchedMatchingHook.targetActionHook)
				{
					var upHook:ActionHook = matchedMatchingHook.targetActionHook;
					matchedDraggingHook.targetActionHook = matchedMatchingHook;
					matchedMatchingHook.targetActionHook = matchedDraggingHook;
					var lastDownHook:DownActionHook = getLastDownHook(matchedDraggingHook.actionComponent);
					if (lastDownHook)
					{
						lastDownHook.targetActionHook = upHook;
						upHook.targetActionHook = lastDownHook;
					}
				}
				else
				{
					matchedDraggingHook.targetActionHook = matchedMatchingHook;
					matchedMatchingHook.targetActionHook = matchedDraggingHook;
				}
				
				// 将拖拽的钩子组件移动到缝合的位置
				var targetPoint:Point = matchedMatchingHook.actionComponent.localToGlobal(new Point(0, 0));
				targetPoint.x = targetPoint.x + matchedMatchingHook.x - matchedDraggingHook.x - 
					matchedDraggingHook.actionComponent.mouseDownX + mouseDownX;
				targetPoint.y = targetPoint.y + matchedMatchingHook.y - matchedDraggingHook.y - 
					matchedDraggingHook.actionComponent.mouseDownY + mouseDownY;
				
				for each (var draggingHookComponent:ActionComponent in draggingHookComponents)
				{
					draggingHookComponent.x = targetPoint.x - mouseDownX + draggingHookComponent.mouseDownX;
					draggingHookComponent.y = targetPoint.y - mouseDownY + draggingHookComponent.mouseDownY;
				}
			}
			
			disposeDraggingHookComponents();
			disposeDraggingFreeHooks();
			disposeMatchingFreeHooks();
			disposeMatchedHook();
			updateHookComponentsSize();  // 更新所有钩子组件大小
			updateHookComponentsPosition(); // 更新所有钩子组件位置
		}
		
		private function initDraggingHookComponents(draggingHookComponent:ActionComponent):void
		{
			// 还原到脚本视图上
			var globalPoint:Point = draggingHookComponent.localToGlobal(new Point(0, 0));
			draggingHookComponent.x = globalPoint.x;
			draggingHookComponent.y = globalPoint.y;
			Application.topApplication.addChild(draggingHookComponent);
			
			draggingHookComponent.mouseDownX = draggingHookComponent.x;
			draggingHookComponent.mouseDownY = draggingHookComponent.y;
			draggingHookComponents.push(draggingHookComponent);
			
			if (draggingHookComponent.downActionHook && draggingHookComponent.downActionHook.targetActionHook)
				initDraggingHookComponents(draggingHookComponent.downActionHook.targetActionHook.actionComponent);
			
			if (draggingHookComponent.downActionHook2 && draggingHookComponent.downActionHook2.targetActionHook)
				initDraggingHookComponents(draggingHookComponent.downActionHook2.targetActionHook.actionComponent);
		}
		
		private function disposeDraggingHookComponents():void
		{
			var draggingHookComponent:ActionComponent;
			var localPoint:Point;
			while (draggingHookComponents.length > 0)
			{
				draggingHookComponent = draggingHookComponents.pop();
				
				// 添加到全局视图 能够全局拖拽
				localPoint = view.globalToLocal(new Point(draggingHookComponent.x, draggingHookComponent.y));
				draggingHookComponent.x = localPoint.x;
				draggingHookComponent.y = localPoint.y;
				view.addChild(draggingHookComponent);
			}
		}
		
		private function initDraggingFreeHooks(draggingHookComponent:ActionComponent):void
		{
			if (draggingHookComponent.upActionHook && !draggingHookComponent.upActionHook.targetActionHook)
				draggingFreeHooks.push(draggingHookComponent.upActionHook);
			
			if (draggingHookComponent.downActionHook)
			{
				if (draggingHookComponent.downActionHook.targetActionHook)
					initDraggingFreeHooks(draggingHookComponent.downActionHook.targetActionHook.actionComponent);
				else
					draggingFreeHooks.push(draggingHookComponent.downActionHook);
			}
			
			if (draggingHookComponent.downActionHook2)
			{
				if (!draggingHookComponent.downActionHook2.targetActionHook)
					draggingFreeHooks.push(draggingHookComponent.downActionHook2);
			}
		}
		
		private function disposeDraggingFreeHooks():void
		{
			var draggingFreeHook:ActionHook;
			while ( draggingFreeHooks.length > 0)
			{
				draggingFreeHook = draggingFreeHooks.pop();
			}
		}
		
		private function initMatchingFreetHooks():void
		{
			var numHookComponent:int = view.numChildren;
			var childHookComponent:ActionComponent;
			for (var index:int = 0; index < numHookComponent; index++)
			{
				childHookComponent = view.getChildAt(index) as ActionComponent;
				if (childHookComponent)
				{
					if (childHookComponent.upActionHook)
						matchingFreeHooks.push(childHookComponent.upActionHook);
					
					if (childHookComponent.downActionHook)
						matchingFreeHooks.push(childHookComponent.downActionHook);
					
					if (childHookComponent.downActionHook2)
						matchingFreeHooks.push(childHookComponent.downActionHook2);
				}
			}
		}
		
		private function disposeMatchingFreeHooks():void
		{
			var matchingFreeHook:ActionHook;
			while ( matchingFreeHooks.length > 0)
			{
				matchingFreeHook = matchingFreeHooks.pop();
			}
		}
		
		private function initMatchedHook(draggingHook:ActionHook, matchingHook:ActionHook):void
		{
			if (draggingHook != matchedDraggingHook ||
				matchingHook != matchedMatchingHook)
			{
				disposeMatchedHook();
				
				matchedDraggingHook = draggingHook;
				matchedMatchingHook = matchingHook;
				
				matchedDraggingHook.matchActionHook = matchedMatchingHook;
				matchedMatchingHook.matchActionHook = matchedDraggingHook;
			}
		}
		
		private function disposeMatchedHook():void
		{
			if (matchedDraggingHook)
			{
				matchedDraggingHook.matchActionHook = null;
				matchedDraggingHook = null;
			}
			if (matchedMatchingHook)
			{
				matchedMatchingHook.matchActionHook = null;
				matchedMatchingHook = null;
			}
		}
		
		private function getLastDownHook(hookComponent:ActionComponent):DownActionHook
		{
			if (hookComponent.downActionHook)
			{
				if (hookComponent.downActionHook.targetActionHook)
					return getLastDownHook(hookComponent.downActionHook.targetActionHook.actionComponent);
				else
					return hookComponent.downActionHook;
			}
			else
				return null;
		}
		
		//----------------------------------------------------------------------------------------------------------------
		//
		// 布局钩子组件
		//
		//----------------------------------------------------------------------------------------------------------------
		
		private function updateHookComponentsSize():void
		{
			var numHookComponent:int = view.numChildren;
			var childHookComponent:ActionComponent;
			for (var index:int = 0; index < numHookComponent; index++)
			{
				childHookComponent = view.getChildAt(index) as ActionComponent;
				if (childHookComponent)
				{
					if (!childHookComponent.upActionHook ||
						!childHookComponent.upActionHook.targetActionHook)
						updateHookComponentSize(childHookComponent);
				}
			}
		}
		
		private function updateHookComponentSize(hookComponent:ActionComponent):void
		{
			if (hookComponent.downActionHook && hookComponent.downActionHook.targetActionHook)
				updateHookComponentSize(hookComponent.downActionHook.targetActionHook.actionComponent);
			
			if (hookComponent.downActionHook2 && hookComponent.downActionHook2.targetActionHook)
				updateHookComponentSize(hookComponent.downActionHook2.targetActionHook.actionComponent);
			
			hookComponent.invalidateSize();
			hookComponent.invalidateDisplayList();
		}
		
		private function updateHookComponentsPosition():void
		{
			var numHookComponent:int = view.numChildren;
			var childHookComponent:ActionComponent;
			for (var index:int = 0; index < numHookComponent; index++)
			{
				childHookComponent = view.getChildAt(index) as ActionComponent;
				if (childHookComponent)
				{
					if (!childHookComponent.upActionHook ||
						!childHookComponent.upActionHook.targetActionHook)
						updateHookComponentPosition(childHookComponent);
				}
			}
		}
		
		private function updateHookComponentPosition(hookComponent:ActionComponent):void
		{
			hookComponent.invalidatePosition();
			
			if (hookComponent.downActionHook && hookComponent.downActionHook.targetActionHook)
				updateHookComponentPosition(hookComponent.downActionHook.targetActionHook.actionComponent);
			
			if (hookComponent.downActionHook2 && hookComponent.downActionHook2.targetActionHook)
				updateHookComponentPosition(hookComponent.downActionHook2.targetActionHook.actionComponent);
		}
		
		//----------------------------------------------------------------------------------------------------------------
		//
		// 钩子匹配失效
		//
		//----------------------------------------------------------------------------------------------------------------
		
		private var invalidateMatchHookFlag:Boolean = false; // 属性失效
		
		public function invalidateMatchHook():void
		{
			if (!invalidateMatchHookFlag)
			{
				invalidateMatchHookFlag = true;
				Application.topApplication.callLater(validateMatchHook);
			}
		}
		
		/**
		 * call validateMatchHook now
		 */        
		public function validateMatchHook():void
		{
			if (invalidateMatchHookFlag)
			{
				invalidateMatchHookFlag = false;
				matchHook();
			}
		}
		
		/**
		 * 匹配钩子
		 */		
		private function matchHook():void
		{
			for each(var draggingFreeHook:ActionHook in draggingFreeHooks)
			{
				for each (var matchingFreeHook:ActionHook in matchingFreeHooks)
				{
					if (matchingFreeHook.match(draggingFreeHook))
					{
						initMatchedHook(draggingFreeHook, matchingFreeHook);
						return;
					}
				}
			}
			
			disposeMatchedHook();
		}
		
	}
}